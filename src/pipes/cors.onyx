package http.server

cors :: struct {
    allowed_origins: [] str;
}

#local
cors_handler :: (c: &cors, req: &Request, res: &Response) {
    origins := string.join(c.allowed_origins, ", ") if c.allowed_origins else "*";
    res.headers["Access-Control-Allow-Origin"] = origins;
    res.headers["Access-Control-Allow-Methods"] = "POST, GET, OPTIONS";

    if req.method == .OPTIONS {
        res.headers["Access-Control-Allow-Headers"] = req.headers["access-control-request-headers"] ?? "";
        res.headers["Access-Control-Max-Age"] = "86400";
        res->status(204);
        res->end();
    }
}

#overload
Pipeable.as_pipe :: (c: &cors) => Pipe.{cors_handler, c};

